apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs.kotlin"

android {
    def globalConfiguration = rootProject.extensions.getByName("ext")

    compileSdkVersion globalConfiguration["android_compile_sdk_version"]
    buildToolsVersion globalConfiguration["android_build_tools_version"]

    defaultConfig {
        minSdkVersion globalConfiguration["android_min_sdk_version"]
        targetSdkVersion globalConfiguration["android_target_sdk_version"]
        multiDexEnabled = true
    }

    dexOptions {
        preDexLibraries = false
        dexInProcess = false
        javaMaxHeapSize "4g"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildFeatures {
        dataBinding = true
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
    }

    lintOptions {
        quiet true
        abortOnError false
        ignoreWarnings true
        disable 'InvalidPackage'            //Some libraries have issues with this.
        disable 'OldTargetApi'              //Lint gives this warning but SDK 20 would be Android L Beta.
        disable 'IconDensities'             //For testing purpose. This is safe to remove.
        disable 'IconMissingDensityFolder'  //For testing purpose. This is safe to remove.
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    api project(':domain')
    api project(':infrastructure')

    /* Kotlin dependencies */
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlin_coroutine_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_coroutine_version"

    /* AndroidX dependencies */
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "androidx.core:core-ktx:$core_ktx_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraint_version"
    implementation "android.arch.lifecycle:extensions:$arch_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.recyclerview:recyclerview:$recyclerview_version"
    implementation "androidx.multidex:multidex:$multidex_version"
    implementation "com.google.android.material:material:$material_version"
    api "androidx.swiperefreshlayout:swiperefreshlayout:$swiperefresh_version"

    /* Navigation component dependencies */
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_version"

    /* Javax dependencies */
    api "javax.annotation:jsr250-api:$javax_annotation_version"
    implementation "javax.inject:javax.inject:$javax_inject_version"

    /* Dagger dependencies */
    api "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    api "com.google.dagger:dagger-android-support:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"

    /* Logging dependencies */
    implementation "com.jakewharton.timber:timber:$timber_version"

    /* Testing dependencies */
    testImplementation "junit:junit:$junit_version"

    androidTestImplementation ("androidx.test.ext:junit:$androidx_junit_version") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"

}